cmake_minimum_required(VERSION 3.12...3.31)
project(social_net_service CXX)

list(APPEND CMAKE_MODULE_PATH "${CMAKE_CURRENT_SOURCE_DIR}/cmake")
include(DownloadUserver)

find_package(
    userver
    COMPONENTS core #
               postgresql
               redis
    QUIET
)
if(NOT userver_FOUND)
    # Tries TRY_DIR first, falls back to downloading userver from GitHub using CPM.
    download_userver(TRY_DIR third_party/userver)
endif()

userver_setup_environment()

# Common sources
include_directories(src)

add_library(
        ${PROJECT_NAME}_objs OBJECT
        src/greeting.cpp #
        #
        src/auth/login.cpp
        src/auth/auth_bearer.cpp
        src/db/data_base.cpp
        src/user/user_register_handler.cpp
        src/user/password_hasher.cpp
        src/user/user_get_handler.cpp
        src/user/user_search_handler.cpp
        src/user/user_search_handler.hpp
        src/friend/friend_add_handler.cpp
        src/friend/friend_add_handler.hpp
        src/friend/friend_delete_handler.cpp
        src/friend/friend_delete_handler.hpp
        src/post/post_create_handler.cpp
        src/post/post_create_handler.hpp
        src/post/post_update_handler.cpp
        src/post/post_update_handler.hpp
        src/post/post_delete_handler.cpp
        src/post/post_delete_handler.hpp
        src/post/post_get_handler.cpp
        src/post/post_get_handler.hpp
        src/post/post_feed_cache.cpp
        src/post/post_feed_cache.hpp
        src/post/post_feed_handler.cpp
        src/post/post_feed_handler.hpp
)
target_link_libraries(
    ${PROJECT_NAME}_objs
    PUBLIC userver::core #
           userver::postgresql
           userver::redis
)


# The Service
add_executable(${PROJECT_NAME} src/main.cpp)
target_link_libraries(${PROJECT_NAME} PRIVATE ${PROJECT_NAME}_objs)

# Unit Tests
add_executable(${PROJECT_NAME}_unittest src/greeting_test.cpp)
target_link_libraries(${PROJECT_NAME}_unittest PRIVATE ${PROJECT_NAME}_objs userver::utest)
add_google_tests(${PROJECT_NAME}_unittest)

# Benchmarks
add_executable(${PROJECT_NAME}_benchmark src/greeting_benchmark.cpp)
target_link_libraries(${PROJECT_NAME}_benchmark PRIVATE ${PROJECT_NAME}_objs userver::ubench)
add_google_benchmark_tests(${PROJECT_NAME}_benchmark)

# Functional testing
userver_testsuite_add_simple()

# Install
include(GNUInstallDirs)

if(DEFINED ENV{PREFIX})
    message(STATUS "Set install prefix: $ENV{PREFIX}")
    file(TO_CMAKE_PATH "$ENV{PREFIX}" PREFIX_PATH)
    set(CMAKE_INSTALL_PREFIX "${PREFIX_PATH}")
endif()

file(GLOB CONFIGS_FILES ${CMAKE_CURRENT_SOURCE_DIR}/configs/*.yaml ${CMAKE_CURRENT_SOURCE_DIR}/configs/*.json)

install(
    TARGETS ${PROJECT_NAME}
    DESTINATION ${CMAKE_INSTALL_BINDIR}
    COMPONENT ${PROJECT_NAME}
)
install(
    FILES ${CONFIGS_FILES}
    DESTINATION ${CMAKE_INSTALL_SYSCONFDIR}/${PROJECT_NAME}
    COMPONENT ${PROJECT_NAME}
)
